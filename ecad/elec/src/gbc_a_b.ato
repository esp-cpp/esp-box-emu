from "generics/buttons.ato" import ButtonPullup, ButtonPulldown

from "generics/interfaces.ato" import Pair, Power
from "generics/buttons.ato" import ButtonPullup, ButtonPulldown

component _GbcABCarbonButton:
    signal in ~ pin 1
    signal out ~ pin 2
    in ~ pin 3
    out ~ pin 4
    footprint = "GBC_A_B_BUTTON"
    # it's a carbon button, so no part number
    mpn = "DNS"

module GbcABButton from ButtonPulldown:
    """
    A module containing an button for the A and B buttons
    from the Gameboy Color
    """
    btn -> _GbcABCarbonButton

module GbcABButtons:
    """
    A module which has A and B buttons
    using the A/B buttons
    """
    power = new Power
    signal a
    signal b
    # create the A and B buttons
    a_btn = new GbcABButton
    b_btn = new GbcABButton
    power ~ a_btn.power
    power ~ b_btn.power
    a ~ a_btn.out
    b ~ b_btn.out

module GbcABXYButtons:
    """
    A module which has A, B, X, and Y buttons
    using the A/B buttons
    """
    power = new Power
    signal a
    signal b
    signal x
    signal y
    a_b_btns = new GbcABButtons
    x_y_btns = new GbcABButtons
    power ~ a_b_btns.power
    power ~ x_y_btns.power
    a ~ a_b_btns.a
    b ~ a_b_btns.b
    x ~ x_y_btns.a
    y ~ x_y_btns.b
