from "atopile/buttons/buttons.ato" import SignalButton, ButtonPullup

component _GbcDpadMembrane:
    footprint = "GAMEBOY_COLOR_DPAD_MEMBRANE"
    mpn = "DNS"

component _GbcDpadCarbonButton:
    signal input ~ pin 1
    signal output ~ pin 2
    input ~ pin 3
    output ~ pin 4
    footprint = "GBC_DPAD_BUTTON"
    # it's a carbon button, so no part number
    mpn = "DNS"

module GbcDpadButton_driver from SignalButton:
    """
    A module containing a button for the D-Pad buttons
    from the Gameboy Color
    """
    package = new _GbcDpadCarbonButton
    input ~ package.input
    output ~ package.output

module GbcDpadButton from ButtonPullup:
    """
    A module containing a button for the D-Pad buttons
    from the Gameboy Color
    """
    button -> GbcDpadButton_driver
    resistance = 10kohm +/- 20%
    # package = "R0402"

module GbcDpad:
    """
    A module that creates a dpad from 4 buttons for the Gameboy Color
    """
    membrane = new _GbcDpadMembrane
    signal up
    signal down
    signal left
    signal right

    # now make the buttons
    up_button = new GbcDpadButton
    up ~ up_button.output
    down_button = new GbcDpadButton
    down ~ down_button.output
    left_button = new GbcDpadButton
    left ~ left_button.output
    right_button = new GbcDpadButton
    right ~ right_button.output
