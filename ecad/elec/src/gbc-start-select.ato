from "atopile/buttons/buttons.ato" import SignalButton, ButtonPullup

component _GbcStartSelectMembrane:
    footprint = "GAMEBOY_COLOR_START_SELECT_MEMBRANE"
    mpn = "DNS"

component _GbcStartSelectCarbonButton_package:
    signal input ~ pin 1
    signal output ~ pin 2
    input ~ pin 3
    output ~ pin 4
    footprint = "GBC_START_SELECT_BUTTON"
    # it's a carbon button, so no part number
    mpn = "DNS"

module GbcStartSelectButton_driver from SignalButton:
    """
    A module containing a button for the D-Pad buttons
    from the Gameboy Color
    """
    package = new _GbcStartSelectCarbonButton_package
    input ~ package.input
    output ~ package.output

module GbcStartSelectButton from ButtonPullup:
    """
    A module containing a start or select button for the GBC
    """
    button -> GbcStartSelectButton_driver
    resistance = 10kohm +/- 20%
    # package = "R0402"

module GbcStartSelect:
    """
    A module containing the start and select buttons
    for the Gameboy Color
    """
    membrane = new _GbcStartSelectMembrane
    start_btn = new GbcStartSelectButton
    select_btn = new GbcStartSelectButton
    signal start ~ start_btn.output
    signal select ~ select_btn.output
